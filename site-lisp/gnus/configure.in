AC_INIT(lisp/gnus.el)
AC_SET_MAKE
AC_PROG_INSTALL

dnl
dnl Apparently, if you run a shell window or a term window in Emacs,
dnl it sets the EMACS environment variable to 't' or a version number
dnl of Emacs.  Lets undo the damage.
dnl
if test "${EMACS}" = "t" -o -n "${INSIDE_EMACS}"; then
   EMACS=""
fi

AC_ARG_WITH(xemacs,
	[AS_HELP_STRING([[--with-xemacs[=PROG]]],
			[use XEmacs to build (default: PROG=xemacs)])],
	[ if test "${withval}" = "yes"; then EMACS=xemacs; else EMACS=${withval}; fi ])
AC_ARG_WITH(emacs,
	[AS_HELP_STRING([[--with-emacs[=PROG]]],
			[use Emacs to build (default: PROG=emacs)])],
	[ if test "${withval}" = "yes"; then EMACS=emacs; else EMACS=${withval}; fi ])
AC_CHECK_PROG(MAKEINFO, makeinfo, makeinfo, no)

AC_CHECK_PROG(EMACS, emacs, emacs, xemacs)

AC_PATH_LISPDIR
AC_PATH_ETCDIR
AC_PATH_INFO_DIR
AC_SET_BUILD_FLAGS
GNUS_CHECK_FONTS

AC_PATH_PROG(GZIP_PROG, gzip)
AC_ARG_WITH(compress-install,
	[AS_HELP_STRING([[--without-compress-install]],
		[do not compress .el and .info files when installing.])],
	[ if test "${withval}" = no; then
		COMPRESS_INSTALL=no;
	  else
		if test -n "${GZIP_PROG}"; then
			COMPRESS_INSTALL=yes;
		else
			COMPRESS_INSTALL=no;
		fi;
	  fi ],
	[ if test -n "${GZIP_PROG}"; then
		COMPRESS_INSTALL=yes;
	  else
		COMPRESS_INSTALL=no;
	  fi ])
AC_SUBST(COMPRESS_INSTALL)

AC_OUTPUT(Makefile etc/Makefile lisp/Makefile texi/Makefile texi/gnusconfig.tex texi/ps/Makefile)
